GLog.d("${project.toString()} -> app")

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Config.compileSdkVersion
    defaultConfig {
        applicationId Config.applicationId + suffix
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName
        multiDexEnabled true
        testInstrumentationRunner rootProject.ext.dependencies.androidJUnitRunner
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", Config.appName + suffix + ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", Config.appName + suffix
        }
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "8g"
        maxProcessCount 8
        dexInProcess = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation rootProject.ext.dependencies.junit
    androidTestImplementation rootProject.ext.dependencies.runner
    androidTestImplementation rootProject.ext.dependencies.espresso_core

    for (def entrySet : ConfigUtil.getApplyPkgs(project.name == 'launcher', project).entrySet()) {
        implementation entrySet.value.dep
    }
}

def getSuffix() {
    if (project.path == ":app:launcher") return ""
    return project.path.replace(":", "_").substring(":core".length(), project.path.length() - ":app".length())
}